# Maintainer: Laurent Soest <laurent.soest(at)gmail.com>

_name='backports.unittest_mock'
pkgname=("python-$_name" "python2-$_name")
pkgver=1.3
pkgrel=1
pkgdesc="Provides a function \"install()\" which makes the \"mock\" module available as \"unittest.mock\" on Python 3.2 and earlier. Also advertises a pytest plugin which configures unittest.mock automatically."
arch=('any')
url="https://github.com/jaraco/backports.unittest_mock"
license=('MIT')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools' 'python-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python-tox' 'python2-tox')
source=("$url/archive/$pkgver.tar.gz")
md5sums=('201bef201aa58ba0ac37096d874daa25')

build() {
    cp -r "${srcdir}/$_name-${pkgver}" "${srcdir}/$_name-${pkgver}-py2"

    # setuptools wont find version from git tag
    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
    cd "$srcdir/$_name-$pkgver"
    python ./setup.py build
    # setuptools wont find version from git tag

    cd "${srcdir}/$_name-${pkgver}-py2"
    python2 ./setup.py build
}

package_python-backports.unittest_mock() {
    depends=('python')
    cd "$srcdir/$_name-$pkgver"
    python ./setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-backports.unittest_mock() {
    depends=('python2')
    cd "$srcdir/$_name-$pkgver-py2"
    python2 ./setup.py install --root="${pkgdir}" --optimize=1
}

check() {
    cd "$srcdir/$_name-$pkgver"
    tox

    cd "$srcdir/$_name-$pkgver-py2"
    tox2
}
